plugins {
    id 'fabric-loom' version '0.2.6-SNAPSHOT'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.3.71'
    id 'com.jfrog.bintray' version '1.8.5'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

sourceSets {
    testmod {
        compileClasspath += sourceSets.main.compileClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath

        java {
            srcDir "src/testmod/java"
        }
        resources {
            srcDir "src/testmod/resources"
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url = "http://maven.fabricmc.net/"
        name = "Fabric"
    }
    maven {
        url = "http://maven.fabricmc.net/io/github/prospector/modmenu"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Mod dependencies
    modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"
    modImplementation "net.fabricmc:fabric-language-kotlin:$fabric_kotlin_version"
    modImplementation "io.github.prospector:modmenu:$modmenu_version"
    modImplementation "me.shedaniel.cloth:config-2:$cloth_config_version"
    modImplementation "me.sargunvohra.mcmods:autoconfig1u:$auto_config_version"

    // Test-only dependencies
    testImplementation "org.junit.jupiter:junit-jupiter:$junit_version"

    // Testmod-only dependencies
    testmodCompile sourceSets.main.output // Inherit from `main`

    modRuntime "me.shedaniel:RoughlyEnoughItems:$rei_version"
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

bintray {
    user = findProperty('bintray.user')
    key = findProperty('bintray.apiKey')
    publications = ['BintrayPublication']
    pkg {
        repo = 'Maven'
        name = 'actionale'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/hnOsmium0001/Actionale.git'
        version {
            name = "$project.version"
            released = new Date()
        }
    }
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            artifact remapJar
            groupId project.group
            artifactId 'actionale'
            version project.version
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
